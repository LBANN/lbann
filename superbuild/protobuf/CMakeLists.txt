enable_language(C)
enable_language(CXX)

# Handle the clone mechanism. First URL
set(PROTOBUF_URL "https://github.com/google/protobuf.git"
  CACHE STRING "The URL from which to clone PROTOBUF")

# ... then the tag.
set(PROTOBUF_TAG "v3.6.1"
  CACHE STRING "The git tag or hash to checkout for PROTOBUF")

# Where to install PROTOBUF
set(PROTOBUF_CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}"
  CACHE PATH "The installation location of PROTOBUF.")

set(PROTOBUF_CMAKE_BUILD_TYPE "${PROTOBUF_CMAKE_BUILD_TYPE}"
  CACHE STRING "The build type for PROTOBUF.")

include(ExternalProject)
ExternalProject_Add(PROTOBUF
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}
  TMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/tmp
  STAMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/stamp
  GIT_REPOSITORY ${PROTOBUF_URL}
  GIT_TAG ${PROTOBUF_TAG}
  SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/src
  SOURCE_SUBDIR cmake
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build
  INSTALL_DIR ${PROTOBUF_CMAKE_INSTALL_PREFIX}
  USES_TERMINAL_BUILD 1
  CMAKE_ARGS
  -G${CMAKE_GENERATOR}
  -DCMAKE_INSTALL_PREFIX=${PROTOBUF_CMAKE_INSTALL_PREFIX}
  -DCMAKE_BUILD_TYPE=${PROTOBUF_CMAKE_BUILD_TYPE}
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_MACOSX_RPATH=ON
  -DCMAKE_CXX_FLAGS="-fPIC"
  )
set(PROTOBUF_DIR ${PROTOBUF_CMAKE_INSTALL_PREFIX}
  CACHE INTERNAL "The install prefix of Protobuf.")
